<letter> ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J"
           | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T"
           | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d"
           | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n"
           | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x"
           | "y" | "z"

<positive digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<digit> ::= "0"
          | <positive digit>

<digits> ::= <digit>
           | <digit> <digits>

<non-digit> ::= <letter>
              | "-"

<identifier character> ::= <digit>
                         | <non-digit>

<identifier characters> ::= <identifier character>
                          | <identifier character> <identifier characters>

<numeric identifier> ::= "0"
                       | <positive digit>
                       | <positive digit> <digits>

<alphanumeric identifier> ::= <non-digit>
    | <non-digit> <identifier characters>
    | <identifier characters> <non-digit>
    | <identifier characters> <non-digit> <identifier characters>

<build identifier> ::= <alphanumeric identifier>
                     | <digits>

<pre-release identifier> ::= <alphanumeric identifier>
                           | <numeric identifier>

<dot-separated build identifiers> ::= <build identifier>
                                    | <build identifier> "." <dot-separated build identifiers>

<build> ::= <dot-separated build identifiers>

<dot-separated pre-release identifiers> ::= <pre-release identifier>
                                          | <pre-release identifier> "." <dot-separated pre-release identifiers>

<pre-release> ::= <dot-separated pre-release identifiers>

<patch> ::= <numeric identifier>

<minor> ::= <numeric identifier>

<major> ::= <numeric identifier>

<version core> ::= <major> "." <minor> "." <patch>

<valid semver> ::= <version core>
                 | <version core> "-" <pre-release>
                 | <version core> "+" <build>
                 | <version core> "-" <pre-release> "+" <build>
